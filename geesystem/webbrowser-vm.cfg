;; -*- mode: scheme; -*-

(use-modules (gnu)
             (gnu system locale)
             (guix)
             (srfi srfi-1))

(use-service-modules desktop mcron networking dbus sound
                     spice xorg sddm)

(use-package-modules bootloaders certs fonts
                     package-management xorg)

(define %geesystem-auto-update-resolution-crutch
  #~(job '(next-second)
         (lambda ()
           (setenv "DISPLAY" ":0.0")
           (setenv "XAUTHORITY" "/home/guest/.Xauthority")
           (execl (string-append #$xrandr "/bin/xrandr") "xrandr" "-s" "0"))
         #:user "guest"))

(operating-system
  (timezone "Asia/Shanghai")
  (keyboard-layout (keyboard-layout "cn"))
  (host-name "Guix")
  (locale "zh_CN.utf8")

  ;; Below we assume /dev/vda is the VM's hard disk.
  ;; Adjust as needed.
  (bootloader (bootloader-configuration
               (bootloader grub-bootloader)
               (targets '("does-not-matter"))))

  (file-systems (cons (file-system
                        (mount-point "/")
                        (device "/dev/vda1")
                        (type "ext4"))
                      %base-file-systems))

  (users (cons (user-account
                (name "guest")
                (comment "guest")
                (password "")                     ;no password
                (group "users")
                (supplementary-groups
                 '("audio" "video")))
               %base-user-accounts))
  
  (packages
   (append
    (map specification->package
         (list
          ;; 桌面管理器
          "ratpoison"

          ;; 网页浏览器
          "ungoogled-chromium"
          "ublock-origin-chromium"

          ;; 字体主题
          "elementary-xfce-icon-theme"
          "font-wqy-microhei"))
    %base-packages))
  
  (services
   (cons*
    ;; Choose SLiM, which is lighter than the default GDM.
    (service slim-service-type
             (slim-configuration
              (auto-login? #t)
              (default-user "guest")
              (xorg-configuration
               (xorg-configuration
                ;; The QXL virtual GPU driver is added to provide
                ;; a better SPICE experience.
                (modules (cons xf86-video-qxl
                               %default-xorg-modules))
                (keyboard-layout keyboard-layout)))))

    ;; Add support for the SPICE protocol, which enables dynamic
    ;; resizing of the guest screen resolution, clipboard
    ;; integration with the host, etc.
    (service spice-vdagent-service-type)

    (simple-service
     'cron-jobs mcron-service-type
     (list %geesystem-auto-update-resolution-crutch))

    (service dhcp-client-service-type)
    (dbus-service)
    (service pulseaudio-service-type)
    (service alsa-service-type)

    %base-services)))
