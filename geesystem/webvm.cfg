;; -*- mode: scheme; -*-

(use-modules (gnu)
             (gnu system locale)
             (guix)
             (guix packages)
             (guix gexp)
             (guix utils)
             (srfi srfi-1)
             (gee packages wm))

(use-service-modules
 desktop mcron networking dbus sound
 spice xorg sddm admin)

(use-package-modules
 bootloaders certs fonts
 package-management ratpoison xorg)

(define %geesystem-auto-update-resolution-crutch
  #~(job '(next-second)
         (lambda ()
           (setenv "DISPLAY" ":0.0")
           (setenv "XAUTHORITY" "/home/guest/.Xauthority")
           (execl (string-append #$xrandr "/bin/xrandr") "xrandr" "-s" "0"))
         #:user "guest"))

(define jwm-webvm
  (package
    (inherit jwm)
    (name "jwm-webvm")
    (arguments
     (substitute-keyword-arguments (package-arguments jwm)
       ((#:phases phases)
        #~(modify-phases #$phases
            (add-after 'install 'install-webvm-files
              (lambda _
                (let* ((etc (string-append #$output "/etc")))
                  (mkdir-p etc)
                  (call-with-output-file (string-append etc "/system.jwmrc")
                    (lambda (port)
                      (format port "\
<?xml version=\"1.0\"?>
<JWM>

  <StartupCommand>mkdir -p ${HOME}/.config/gtk-3.0</StartupCommand>
  <StartupCommand>cp -f /etc/gtk3-settings.ini ${HOME}/.config/gtk-3.0/settings.ini</StartupCommand>
  <StartupCommand>pkill -9 chromium; chromium</StartupCommand>

  <RestartCommand>pkill -9 chromium; chromium</RestartCommand>

  <RootMenu onroot=\"123\">
    <Program label=\"1. Open Web Browser \">pkill -9 chromium; chromium</Program>
    <Separator/>
    <Program label=\"2. Open Terminal\">xterm</Program>
    <Separator/>
    <Restart label=\"3. Restart JWM\"/>
    <Separator/>
    <Program label=\"4. Shutdown\">sudo halt</Program>
  </RootMenu>

  <MenuStyle>
    <Font>-*-helvetica-medium-r-*-*-24-*-*-*-*-*-*-*</Font>
  </MenuStyle>

  <Group>
    <Option>maximized</Option>
    <Option>nomove</Option>
    <Option>noborder</Option>
    <Option>notitle</Option>
  </Group>

</JWM>"))))))))))))

(operating-system
  (timezone "Asia/Shanghai")
  (keyboard-layout (keyboard-layout "cn"))
  (host-name "Guix-WebVM")
  (locale "zh_CN.utf8")

  ;; Below we assume /dev/vda is the VM's hard disk.
  ;; Adjust as needed.
  (bootloader (bootloader-configuration
               (bootloader grub-bootloader)
               (targets '("does-not-matter"))))

  (file-systems (cons (file-system
                        (mount-point "/")
                        (device "/dev/vda1")
                        (type "ext4"))
                      %base-file-systems))

  (users (cons (user-account
                (name "guest")
                (comment "guest")
                (password "")                     ;no password
                (group "users")
                (supplementary-groups
                 '("audio")))
               %base-user-accounts))

  ;; Our /etc/sudoers file.  Since 'guest' initially has an empty password,
  ;; allow for password-less sudo.
  (sudoers-file (plain-file "sudoers" "\
root ALL=(ALL) ALL
guest ALL=NOPASSWD: /run/current-system/profile/sbin/halt\n"))

  (packages
   (append
    (map specification->package
         (list
          ;; 终端
          "xterm"
          "xfontsel"

          ;; 网页浏览器
          "ungoogled-chromium"
          "ublock-origin-chromium"

          ;; 字体主题
          "elementary-xfce-icon-theme"
          "font-wqy-microhei"))
    (list jwm-webvm)
    %base-packages))

  (services
   (cons*
    (service special-files-service-type
             `(("/etc/gtk3-settings.ini",
                (mixed-text-file "gtk3-settings.ini" "\
[Settings]
gtk-theme-name=Adwaita
gtk-icon-theme-name=elementary-xfce
gtk-font-name=WenQuanYi Micro Hei 16
gtk-cursor-theme-name=Adwaita
gtk-cursor-theme-size=16"))))

    ;; Choose SLiM, which is lighter than the default GDM.
    (service slim-service-type
             (slim-configuration
              (auto-login? #t)
              (default-user "guest")
              (xorg-configuration
               (xorg-configuration
                (modules (list
                          ;; The QXL virtual GPU driver is added to provide a
                          ;; better SPICE experience.
                          xf86-video-qxl
                          xf86-video-vesa
                          xf86-input-libinput
                          xf86-input-evdev
                          xf86-input-keyboard
                          xf86-input-mouse))
                (keyboard-layout keyboard-layout)))))

    ;; Add support for the SPICE protocol, which enables dynamic
    ;; resizing of the guest screen resolution, clipboard
    ;; integration with the host, etc.
    (service spice-vdagent-service-type)

    (simple-service
     'cron-jobs mcron-service-type
     (list %geesystem-auto-update-resolution-crutch))

    (service dhcp-client-service-type)
    (dbus-service)
    (service pulseaudio-service-type)
    (service alsa-service-type)

    (modify-services %base-services
      (delete login-service-type)
      (delete virtual-terminal-service-type)
      (delete mingetty-service-type)
      (delete agetty-service-type)
      (delete console-font-service-type)))))
