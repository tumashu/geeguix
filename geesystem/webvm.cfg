;; -*- mode: scheme; -*-

(use-modules (gnu)
             (gnu system locale)
             (guix)
             (guix packages)
             (guix utils)
             (srfi srfi-1))

(use-service-modules
 desktop mcron networking dbus sound
 spice xorg sddm admin)

(use-package-modules
 bootloaders certs fonts
 package-management ratpoison xorg)

(define %geesystem-auto-update-resolution-crutch
  #~(job '(next-second)
         (lambda ()
           (setenv "DISPLAY" ":0.0")
           (setenv "XAUTHORITY" "/home/guest/.Xauthority")
           (execl (string-append #$xrandr "/bin/xrandr") "xrandr" "-s" "0"))
         #:user "guest"))

(define-public ratpoison-webvm
  (package
    (inherit ratpoison)
    (name "ratpoison-webvm")
    (arguments
     (substitute-keyword-arguments (package-arguments ratpoison)
       ((#:phases phases)
        #~(modify-phases #$phases
            (delete 'install-xsession)
            (add-after 'install 'install-webvm-files
              (lambda _
                (let* ((xsessions (string-append #$output "/share/xsessions"))
                       (sharedir  (string-append #$output "/share/ratpoison"))
                       (cfgdir    (string-append sharedir "/webvm")))
                  (mkdir-p xsessions)
                  (mkdir-p cfgdir)
                  (call-with-output-file (string-append cfgdir "/gtk3-settings.ini")
                    (lambda (port)
                      (format port
                              "[Settings]~@
                               gtk-theme-name=Adwaita~@
                               gtk-icon-theme-name=gnome~@
                               gtk-font-name=WenQuanYi Micro Hei 16~@
                               gtk-cursor-theme-name=Adwaita~@
                               gtk-cursor-theme-size=18")))

                  (call-with-output-file (string-append cfgdir "/ratpoisonrc")
                    (lambda (port)
                      (format port
                              "exec ~@*~a -c 'mkdir -p ${HOME}/.config/gtk-3.0'~@
                               exec ~@*~a -c 'cp -f ~1@*~a/gtk3-settings.ini ${HOME}/.config/gtk-3.0/settings.ini'~@
                               exec ~@*~a -c 'exec chromium'~@
                               bind C-c exec ~@*~a -c 'pkill -9 chromium; exec chromium'~@
                               bind C-q exec ~@*~a -c 'sudo halt'"
                              "/run/current-system/profile/bin/bash" cfgdir)))

                  (call-with-output-file (string-append xsessions "/ratpoison-webvm.desktop")
                    (lambda (port)
                      (format port
                              "[Desktop Entry]~@
                               Name=ratpoison-webvm~@
                               Comment=Tiling window manager: say goodbye to the rodent!~@
                               Exec=~a/bin/ratpoison -f ~a/ratpoisonrc~@
                               TryExec=~@*~a/bin/ratpoison~@
                               Type=Application~%"
                              #$output cfgdir))))))))))))

(operating-system
  (timezone "Asia/Shanghai")
  (keyboard-layout (keyboard-layout "cn"))
  (host-name "Guix-WebVM")
  (locale "zh_CN.utf8")

  ;; Below we assume /dev/vda is the VM's hard disk.
  ;; Adjust as needed.
  (bootloader (bootloader-configuration
               (bootloader grub-bootloader)
               (targets '("does-not-matter"))))

  (file-systems (cons (file-system
                        (mount-point "/")
                        (device "/dev/vda1")
                        (type "ext4"))
                      %base-file-systems))

  (users (cons (user-account
                (name "guest")
                (comment "guest")
                (password "")                     ;no password
                (group "users")
                (supplementary-groups
                 '("audio" "video")))
               %base-user-accounts))

  ;; Our /etc/sudoers file.  Since 'guest' initially has an empty password,
  ;; allow for password-less sudo.
  (sudoers-file (plain-file "sudoers" "\
root ALL=(ALL) ALL
guest ALL=NOPASSWD: /run/current-system/profile/sbin/halt\n"))

  (packages
   (append
    (map specification->package
         (list
          ;; 网页浏览器
          "ungoogled-chromium"
          "ublock-origin-chromium"

          ;; 字体主题
          "elementary-xfce-icon-theme"
          "font-wqy-microhei"))
    ;; 桌面管理器
    (list ratpoison-webvm)
    %base-packages))

  (services
   (cons*
    ;; Choose SLiM, which is lighter than the default GDM.
    (service slim-service-type
             (slim-configuration
              (auto-login? #t)
              (default-user "guest")
              (xorg-configuration
               (xorg-configuration
                (modules (list
                          ;; The QXL virtual GPU driver is added to provide a
                          ;; better SPICE experience.
                          xf86-video-qxl
                          xf86-video-vesa
                          xf86-input-libinput
                          xf86-input-evdev
                          xf86-input-keyboard
                          xf86-input-mouse))
                (keyboard-layout keyboard-layout)))))

    ;; Add support for the SPICE protocol, which enables dynamic
    ;; resizing of the guest screen resolution, clipboard
    ;; integration with the host, etc.
    (service spice-vdagent-service-type)

    (simple-service
     'cron-jobs mcron-service-type
     (list %geesystem-auto-update-resolution-crutch))

    (service dhcp-client-service-type)
    (dbus-service)
    (service pulseaudio-service-type)
    (service alsa-service-type)

    (modify-services %base-services
      (delete login-service-type)
      (delete virtual-terminal-service-type)
      (delete mingetty-service-type)
      (delete agetty-service-type)
      (delete console-font-service-type)
      (delete rottlog-service-type)
      (delete log-cleanup-service-type)
      (delete static-networking-service-type)))))
